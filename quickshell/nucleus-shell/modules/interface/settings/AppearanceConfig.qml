import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import Quickshell.Widgets
import qs.config
import qs.modules.widgets
import qs.services

ContentMenu {
    title: "Appearance"
    description: "Adjust how the desktop looks like."

    ContentCard {
        ColumnLayout {
            Layout.fillWidth: true
            spacing: 16

            ColumnLayout {
                spacing: 4

                StyledText {
                    text: "Select Theme"
                    font.pixelSize: 16
                }

                StyledText {
                    text: "Choose between dark or light mode."
                    font.pixelSize: 12
                    color: "#888888"
                }
            }

            RowLayout {
                Layout.leftMargin: 15
                Layout.fillWidth: true
                Layout.alignment: Qt.AlignHCenter
                spacing: 16

                StyledButton {
                    Layout.preferredHeight: 300
                    Layout.preferredWidth: 460
                    Layout.maximumHeight: 400
                    Layout.maximumWidth: 500
                    icon: "dark_mode"
                    iconSize: 64
                    checked: Config.runtime.appearance.theme === "dark"
                    hoverEnabled: true

                    onClicked: {
                        Config.updateKey("appearance.theme", "dark")

                        if (!Config.runtime.appearance.colors.autogenerated) {
                            const scheme = Config.runtime.appearance.colors.scheme
                            const file = presetGrid.themeMap[scheme]?.dark

                            if (file) {
                                Quickshell.execDetached([
                                    "bash",
                                    Directories.scriptsPath + "/interface/switchTheme.sh",
                                    file
                                ])
                            }
                        } else {
                            Quickshell.execDetached([
                                "qs", "-c", "nucleus-shell", "ipc", "call", "global", "regenColors"
                            ])
                        }
                    }
                }

                StyledButton {
                    Layout.preferredHeight: 300
                    Layout.preferredWidth: 460
                    Layout.maximumHeight: 400
                    Layout.maximumWidth: 500
                    icon: "light_mode"
                    iconSize: 64
                    checked: Config.runtime.appearance.theme === "light"
                    hoverEnabled: true

                    onClicked: {
                        Config.updateKey("appearance.theme", "light")

                        if (!Config.runtime.appearance.colors.autogenerated) {
                            const scheme = Config.runtime.appearance.colors.scheme
                            const file = presetGrid.themeMap[scheme]?.light

                            if (file) {
                                Quickshell.execDetached([
                                    "bash",
                                    Directories.scriptsPath + "/interface/switchTheme.sh",
                                    file
                                ])
                            }
                        } else {
                            Quickshell.execDetached([
                                "qs", "-c", "nucleus-shell", "ipc", "call", "global", "regenColors"
                            ])
                        }
                    }
                }
            }
        }

        ColumnLayout {
            StyledText {
                text: "Color Generation Schemes:"
                font.pixelSize: 16
            }

            GridLayout {
                rows: 2
                columns: 4

                Repeater {
                    model: [
                        "scheme-content",
                        "scheme-expressive",
                        "scheme-fidelity",
                        "scheme-fruit-salad",
                        "scheme-monochrome",
                        "scheme-neutral",
                        "scheme-rainbow",
                        "scheme-tonal-spot"
                    ]

                    delegate: StyledButton {
                        text: modelData
                        clip: true
                        Layout.fillWidth: true
                        implicitWidth: 0
                        checked: Config.runtime.appearance.colors.matugenScheme === modelData
                        enabled: Config.runtime.appearance.colors.autogenerated

                        topLeftRadius: index === 0 ? Appearance.rounding.normal : Appearance.rounding.small
                        bottomLeftRadius: index === 0 ? Appearance.rounding.normal : Appearance.rounding.small
                        topRightRadius: index === (model.count - 1) ? Appearance.rounding.normal : Appearance.rounding.small
                        bottomRightRadius: index === (model.count - 1) ? Appearance.rounding.normal : Appearance.rounding.small

                        onClicked: {
                            Config.updateKey(
                                "appearance.colors.matugenScheme",
                                modelData
                            )

                            Quickshell.execDetached([
                                "qs", "-c", "nucleus-shell", "ipc", "call", "global", "regenColors"
                            ])
                        }
                    }
                }
            }
        }

        ColumnLayout {
            StyledText {
                font.pixelSize: 16
                text: "Predefined/Custom Themes:"
            }

            GridLayout {
                id: presetGrid

                property var themeMap: ({ })

                rows: 2
                columns: 4

                Repeater {
                    model: Object.keys(presetGrid.themeMap)

                    delegate: StyledButton {
                        text: modelData
                        Layout.fillWidth: true
                        implicitWidth: 0
                        checked: Config.runtime.appearance.colors.scheme === modelData
                        enabled: !Config.runtime.appearance.colors.autogenerated

                        onClicked: {
                            const variant = Config.runtime.appearance.theme
                            const file = presetGrid.themeMap[modelData][variant]

                            if (!file)
                                return

                            Config.updateKey(
                                "appearance.colors.scheme",
                                modelData
                            )

                            Quickshell.execDetached([
                                "bash",
                                Directories.scriptsPath + "/interface/switchTheme.sh",
                                file
                            ])
                        }
                    }
                }

                Process {
                    id: loadThemes
                    command: ["ls", Directories.shellConfig + "/colorschemes"]
                    running: true

                    stdout: StdioCollector {
                        onStreamFinished: {
                            const map = { }

                            text.split("\n")
                                .map(t => t.trim())
                                .filter(t => t.endsWith(".json"))
                                .forEach(t => {
                                    const name = t.replace(/\.json$/, "")
                                    const parts = name.split("-")
                                    const variant = parts.pop()
                                    const base = parts.join("-")

                                    if (!map[base])
                                        map[base] = { }

                                    map[base][variant] = name
                                })

                            presetGrid.themeMap = map
                        }
                    }
                }
            }
        }
    }

    ContentCard {
        StyledSwitchOption {
            title: "Tint Icons"
            description: "Either tint icons across the shell or keep them colorized."
            prefField: "appearance.tintIcons"
        }

        StyledSwitchOption {
            title: "Use Autogenerated Themes"
            description: "Use autogenerated themes."
            prefField: "appearance.colors.autogenerated"
        }
    }

    ContentCard {
        StyledText {
            text: "Clock"
            font.pixelSize: 20
            font.bold: true
        }

        StyledSwitchOption {
            title: "Show Clock"
            description: "Whether to show or disable the clock on the background."
            prefField: "appearance.background.clock.enabled"
        }

        StyledText {
            text: "Clock Position"
            font.pixelSize: 16
            font.bold: true
        }

        RowLayout {
            id: clockPositionSelector

            ColumnLayout {
                StyledText {
                    text: "Clock Position"
                    font.pixelSize: 16
                }

                StyledText {
                    text: "Choose where the clock appears on the background."
                    font.pixelSize: 12
                }
            }

            Item {
                Layout.fillWidth: true
            }

            StyledDropDown {
                label: "Position"
                model: [
                    "Top Left",
                    "Top Right",
                    "Bottom Left",
                    "Bottom Right"
                ]

                currentIndex: {
                    switch (Config.runtime.appearance.background.clock.position.toLowerCase()) {
                    case "top-left": return 0
                    case "top-right": return 1
                    case "bottom-left": return 2
                    case "bottom-right": return 3
                    default: return 0
                    }
                }

                onSelectedIndexChanged: (index) => {
                    Config.updateKey(
                        "appearance.background.clock.position",
                        model[index].toLowerCase().replace(" ", "-")
                    )
                }
            }
        }
    }
}
